emulate -LR zsh

zmodload zsh/mathfunc

integer width=10 height=10

# Old system state
typeset -A Ao Bo

# New system state
typeset -A An Bn

# Helper variables
integer index colidx
float color
float shade
float tmp1 tmp2

# Configurations
integer crowsize=7
configs=( #CA#  #CB#   #iter.# #internal mul.#   #factor#  #smooth mul."   #a, b, both#
          10.0  23.39  14      0.005              1.422    0.3             3
          10.0  23.39  14      0.0105             1.422    0.3             3
          10.0  23.39  14      0.0105             0.1422   0.5             2
) 

float CA=10.0
float CB=23.39
integer C_iter
float C_imul C_factor C_smul
integer C_abboth

# solve_one_iteration* vars
integer i j n i_add1 i_sub1 j_add1 j_sub1
float DiA ReA DiB ReB
float Aval Bval
float Anext_x Anext_y Aprev_x Aprev_y
float Bnext_x Bnext_y Bprev_x Bprev_y

# One iteration on An, Bn
# {{{
solve_one_iteration() {
    for (( i=1; i<=height; i ++ )); do
        i_add1=1+(i%height)
        i_sub1=i-1
        (( i_sub1 < 1 )) && i_sub1=height

        for (( j=1; j<=width; j ++ )); do
            j_add1=1+(j%width)
            j_sub1=j-1
            (( j_sub1 < 1 )) && j_sub1=width

            # const double Aval = m_Ao[i][j], Bval = m_Bo[i][j];
            index=i*width+j
            Aval=${Ao[$index]}
            Bval=${Bo[$index]}

            index=i*width+j_add1
            Bnext_x=${Bo[$index]}
            Anext_x=${Ao[$index]}

            index=i_add1*width+j
            Anext_y=${Ao[$index]}
            Bnext_y=${Bo[$index]}

            index=i*width+j_sub1
            Aprev_x=${Ao[$index]}
            Bprev_x=${Bo[$index]}

            index=i_sub1*width+j
            Aprev_y=${Ao[$index]}
            Bprev_y=${Bo[$index]}

            # Substance A - its diffusion process
            DiA=$(( CA * ( Anext_x - 2.0 * Aval + Aprev_x   +   Anext_y - 2.0 * Aval + Aprev_y ) ))
            # ... its reaction process
            ReA=$(( Aval * Bval - Aval - 12.0 ))

            # Euler step
            index=i*width+j
            An[$index]=$(( Aval + C_imul * (ReA + DiA) ))

            # Substance B - its diffusion process
            DiB=$(( CB * ( Bnext_x - 2.0 * Bval + Bprev_x   +   Bnext_y - 2.0 * Bval + Bprev_y ) ))
            # ... its reaction process
            ReB=$(( 16.0 - Aval * Bval ))

            # Euler step
            Bn[$index]=$(( Bval + C_imul * (ReB + DiB) ))
        done
    done
}
# }}}

# One iteration on Ao, Bo
# {{{
solve_one_iteration_reversed() {
    for (( i=1; i<=height; i ++ )); do
        i_add1=1+(i%height)
        i_sub1=i-1
        (( i_sub1 < 1 )) && i_sub1=height

        for (( j=1; j<=width; j ++ )); do
            j_add1=1+(j%width)
            j_sub1=j-1
            (( j_sub1 < 1 )) && j_sub1=width

            # const double Aval = m_Ao[i][j], Bval = m_Bo[i][j];
            index=i*width+j
            Aval=${An[$index]}
            Bval=${Bn[$index]}

            index=i*width+j_add1
            Anext_x=${An[$index]}
            Bnext_x=${Bn[$index]}

            index=i_add1*width+j
            Anext_y=${An[$index]}
            Bnext_y=${Bn[$index]}

            index=i*width+j_sub1
            Aprev_x=${An[$index]}
            Bprev_x=${Bn[$index]}

            index=i_sub1*width+j
            Aprev_y=${An[$index]}
            Bprev_y=${Bn[$index]}

            # Substance A - its diffusion process
            DiA=$(( CA * ( Anext_x - 2.0 * Aval + Aprev_x   +   Anext_y - 2.0 * Aval + Aprev_y ) ))
            # ... its reaction process
            ReA=$(( Aval * Bval - Aval - 12.0 ))

            # Euler step
            index=i*width+j
            Ao[$index]=$(( Aval + C_imul * (ReA + DiA) ))

            # Substance B - its diffusion process
            DiB=$(( CB * ( Bnext_x - 2.0 * Bval + Bprev_x   +   Bnext_y - 2.0 * Bval + Bprev_y ) ))
            # ... its reaction process
            ReB=$(( 16.0 - Aval * Bval ))

            # Euler step
            Bo[$index]=$(( Bval + C_imul * (ReB + DiB) ))
        done
    done
}
# }}}

# Main loop
# {{{
solve() {
    # Iterations
    for (( n=1; n<=C_iter; n ++ )); do
        solve_one_iteration
        solve_one_iteration_reversed
    done
}
# }}}

# Initial randomization
# {{{
randomize() {
    integer i j
    float prev1=0.0 prev2=0.0
    float new1 new2

    for (( i=1; i<=height; i ++ )); do
        for (( j=1; j<=width; j ++ )); do
            new1=$(( rand48() ))
            if (( new1 > prev1 * C_factor )); then
                new1=new1*C_smul
            elif (( new1 < prev1 / C_factor )); then
                new1=new1/C_smul
            fi

            new2=$(( rand48() ))
            if (( new2 > prev2 * C_factor )); then
                new2=new2*C_smul
            elif (( new2 < prev2 / C_factor )); then
                new2=new2/C_smul
            fi

            # Store into the array
            index=i*width+j
            Ao[$index]=$(( new1*30.0 ))
            Bo[$index]=$(( new2*30.0 ))

            prev1=new1
            prev2=new2
        done
    done
}
# }}}

# Dump values
# {{{
dump() {
    print
    for (( i=1; i<=height; i ++ )); do
        for (( j=1; j<=width; j ++ )); do
            index=i*width+j
            LANG=C printf "(%.1f,%.1f)" ${Ao[$index]} ${Bo[$index]}
        done
        print
    done
}
# }}}

# Draw
# {{{
render() {
    print
    # Get maximum average
    float max=0.0
    for (( i=1; i<=height; i ++ )); do
        for (( j=1; j<=width; j ++ )); do
            index=i*width+j
            if (( C_abboth == 1 )); then
                tmp1=${Ao[$index]}
                tmp2=${Ao[$index]}
            elif (( C_abboth == 2 )); then
                tmp1=${Bo[$index]}
                tmp2=${Bo[$index]}
            else
                tmp1=${Ao[$index]}
                tmp2=${Bo[$index]}
            fi
            tmp1=$(( (tmp1+tmp2) / 2.0 ))

            if (( max < tmp1 )); then
                max=tmp1
            fi
        done
    done

    for (( i=1; i<=height; i ++ )); do
        for (( j=1; j<=width; j ++ )); do
            index=i*width+j
            if (( C_abboth == 1 )); then
                tmp1=${Ao[$index]}
                tmp2=${Ao[$index]}
            elif (( C_abboth == 2 )); then
                tmp1=${Bo[$index]}
                tmp2=${Bo[$index]}
            else
                tmp1=${Ao[$index]}
                tmp2=${Bo[$index]}
            fi
            tmp1=$(( (tmp1+tmp2) / 2.0 ))

            # 256-color variations
            #(( color=ceil( (tmp1/max)*214.99) ))
            #colidx=color+232

            #(( color=ceil( (tmp1/max)*20.99) ))
            #colidx=color+232

            #(( color=ceil( (tmp1/max)*5.99) ))
            #(( shade=ceil( (tmp1/max)*34.99) ))
            #colidx=color+shade*6+16

            #(( color=ceil( (tmp1/max)*215.99) ))
            #colidx=color
            #colidx=$colortable4[colidx]

            #(( color=ceil( (tmp1/max)*89.99) ))
            #colidx=color
            #colidx=$colortable4[colidx]
            
            #echo -n "\\e[48;5;${colidx}m "

            # 8 colors
            (( color=fabs(ceil(tmp1/max*7.99)) ))
            colidx=$color
            echo -n "\\e[4${colidx}m "

        done
        echo "\\e[0m"
    done
}
# }}}

#
# Main code
#

width=$(( COLUMNS -1 ))
height=$(( LINES -2 ))

#
# Select configuration
#

integer crows=$(( ${#configs} / crowsize ))
integer crow=$(( (RANDOM % crows) + 1 ))
#crow=1
integer cidx=$(( (crow-1)*crowsize + 1 ))

#echo "crows $crows, crowsize $crowsize , crow $crow, cidx $cidx"

CA=$configs[cidx]
CB=$configs[cidx+1]
C_iter=$configs[cidx+2]
C_imul=$configs[cidx+3]
C_factor=$configs[cidx+4]
C_smul=$configs[cidx+5]
C_abboth=$configs[cidx+6]

#echo "Config is: CA $CA, CB $CB, C_iter $C_iter, C_imul $C_imul, C_factor $C_factor, C_smul $C_smul, C_abboth $C_abboth"

randomize
solve
render

# vim:ft=zsh
