emulate -LR zsh

# Draws mandelbrot fractal {{{1
function -morpho-mandelbrot {
   integer lines columns colour i=0
   float a b p q pnew

   columns=COLUMNS-1
   lines=LINES-1

   for (( b=-1.5; b<=1.5; b+=3.0/lines )) do
       for (( a=-2.0; a<=1; a+=3.0/columns )) do
           for (( p=0.0, q=0.0, i=0; p*p+q*q < 4 && i < 32; i++ )) do
               (( pnew=p*p-q*q+a, q=2*p*q+b, p=pnew ))
           done
           ((colour=(i/4)%8))
            echo -n "\\e[4${colour}m "
        done
        echo "\\e[0m"
    done
}
# 1}}}

#
# Main code
#

# Standard screen saver before-loop block
autoload morlib
morlib
setopt localtraps
-morlib_alternate_screen 1
trap "return" TERM INT QUIT
trap "-morlib_alternate_screen 0" EXIT
integer redraw_delay
zstyle -s ':morpho' redraw-delay redraw_delay || redraw_delay=300
# End of standard screen saver before-loop block

# Main loop
while (( 1 )); do
    -morpho-mandelbrot

    sleep $redraw_delay
done

# vim:ft=zsh
